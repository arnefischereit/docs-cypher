:description: The `CREATE` clause is used to create nodes and relationships.

[[query-create]]
= CREATE

== Introduction

The `CREATE` clause allows you to create nodes and relationships.
Similar to xref::clauses/match.adoc[`MATCH`], this is done using xref::patterns/index.adoc[patterns].
However, the patterns you can use in `CREATE` are restricted to what is relevant to describe the newly created nodes and relationships.

[[create-nodes]]
== Syntax for nodes

The `CREATE` clause allows you to create one or more nodes.
For each, you can specify labels and properties.
You can give the nodes names that you can refer to later in the query.
xref:clauses/where.adoc[`WHERE`-subclauses] and xref:clauses/match.adoc#label-expression-match-or-expression[label expression] are not allowed.
Instead, multiple labels are separated by colons.

.Query
[source, cypher, indent=0]
----
CREATE (charlie:Person:Actor {name: 'Charlie Sheen'}), (oliver:Person:Director {name: 'Oliver Stone'})
----

Creates two nodes, each with a `name` and a label `Person`.
The one representing Charlie Sheen also has the label `Actor` while the one representing Oliver Stone receives the label `Director`.

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Nodes created: 2 +
Properties set: 2 +
Labels added: 4
|===

[[create-relationships]]
== Syntax for relationships

You can create relationships very similarly to nodes.
However, relationships always need exactly one relationship type and a direction.
Beyond this, they may also have properties and names.
They may not be quantified or carry `WHERE`-subclause.

.Query
[source, cypher, indent=0]
----
CREATE (charlie:Person:Actor {name: 'Charlie Sheen'})-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet:Movie {title: 'Wall Street'})<-[:DIRECTED]-(oliver:Person:Director {name: 'Oliver Stone'})
----

Creates not only the nodes for Charlie Sheen and Oliver Stone as well as the one for the movie Wall Street but also the relationships of type `ACTED_IN` and `DIRECTED` between them.

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Nodes created: 3 +
Relationships created: 2 +
Properties set: 5 +
Labels added: 5
|===

[[reusing-variables]]
== Reusing variables
The previous example created a whole path at once.
Note, that these newly created elements are now disconnected from the rest of the graph.
To instead connect them to already existing data, you can refer to variables from other parts of the query.

.Query
[source, cypher, indent=0]
----
MATCH (charlie:Person {name: 'Charlie Sheen'}), (oliver:Person {name: 'Oliver Stone'})
CREATE (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet:Movie {title: 'Wall Street'})<-[:DIRECTED]-(oliver)
----

First looks up the two nodes representing Charlie Sheen and Oliver Stone in order to then connect them in the same way as we have done in the previous query.

You can also reuse variables from the same `CREATE`, both in the same or a later clause.
This way, you can, for example, define constructs that are more complex than just a linear path.

.Query
[source, cypher, indent=0]
----
CREATE p = (charlie:Person:Actor {name: 'Charlie Sheen'})-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet:Movie {title: 'Wall Street'})<-[:DIRECTED]-(oliver:Person:Director {name: 'Oliver Stone'}), (wallStreet)<-[:ACTED_IN {role: 'Gordon Gekko'}]-(michael:Person:Actor {name: 'Michael Douglas'})
RETURN length(p)
----

Creates all three nodes for Charlie Sheen, Oliver Stone and Michael Douglas and connects them all to the node representing the Wall Street movie.
It then returns the length of the path from Charlie Sheen to Oliver Stone.

Note that when repeating a node's name, you may not apply labels or properties to the repetition.

.Query
[source, cypher, role=test-fail]
----
MATCH (charlie:Person {name: 'Charlie Sheen'})
CREATE (charlie:Actor)
----

This query will fail because the node `charlie` already exists, and we therefore cannot `CREATE` it.
If you intend to add a label, use the xref:clauses/set.adoc#set-set-a-label-on-a-node[`SET` clause] instead.

[[reusing-variables-properties]]
== Reusing variables in properties
The value that can be assigned to a node's or a relationship's property can be defined by an xref::syntax/expressions.adoc[expression].

////
.Parameters
[source,javascript, role=test-setup]
----
{
  "age": 23
}
----
////

.Query
[source, cypher, indent=0]
----
MATCH (person:Person)
  WHERE person.name IS NOT NULL
CREATE (anotherPerson:Person {name: person.name, age: $age})
----

Creates a person with the same name as another person and the age from a xref:syntax/parameters.adoc[parameter] called `age`.

Such an expression may not contain a reference to a variable that is defined in the same `CREATE` statement.
This is defined this way so that it is always clear what value a property should have.

.Query
[source, cypher, role=test-fail]
----
CREATE (charlie {score: oliver.score + 1}), (oliver {score: charlie.score + 1})
----

Tries to create nodes such that Charlie's score is higher than Oliver's and vice versa, which is a contradiction.
Therefore, queries like this are rejected.

[[use-parameters-with-create]]
== Use parameters with `CREATE`

[[create-create-node-with-a-parameter-for-the-properties]]
=== Create node with a parameter for the properties

You can also create a graph entity from a map.
All the key/value pairs in the map will be set as properties on the created relationship or node.
In this case we add a `Person` label to the node as well.

.Parameters
[source,javascript, indent=0]
----
{
  "props": {
    "name": "Andy",
    "position": "Developer"
  }
}
----

.Query
[source, cypher]
----
CREATE (n:Person $props)
RETURN n
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +n+
| +Node[2]{name:"Andy",position:"Developer"}+
1+d|Rows: 1 +
Nodes created: 1 +
Properties set: 2 +
Labels added: 1
|===


[[create-create-multiple-nodes-with-a-parameter-for-their-properties]]
=== Create multiple nodes with a parameter for their properties

By providing Cypher an array of maps, it will create a node for each map.

.Parameters
[source,javascript, indent=0]
----
{
  "props": [ {
    "name": "Andy",
    "position": "Developer"
  }, {
    "name": "Michael",
    "position": "Developer"
  } ]
}
----

.Query
[source, cypher, indent=0]
----
UNWIND $props AS map
CREATE (n)
SET n = map
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Nodes created: 2 +
Properties set: 4
|===

